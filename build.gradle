buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: "java"
apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "maven-publish"
apply plugin: "bintray"

repositories {
    mavenCentral()
}

dependencies {
    // TODO: cut this down to just what is really needed
    compile "org.apache.jclouds:jclouds-all:1.6.2-incubating"
    compile "org.mongodb:mongo-java-driver:2.11.3"
    testCompile "org.codehaus.groovy:groovy-all:2.1.7"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

sourceCompatibility = "1.7"

group = "com.commercehub.jclouds"
version = "1.0.0-SNAPSHOT"

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    publications = ["mavenJava"]
    pkg {
        repo = "main"
        userOrg = "commercehub-oss"
        name = "jclouds-gridfs-blobstore"
        desc = "A JClouds BlobStore provider backed by MongoDB's GridFS"
        licenses = ['Apache-2.0']
        labels = ["mongodb", "jclouds", "gridfs"]
    }
}
bintrayUpload.dependsOn build, sourceJar, javadocJar
